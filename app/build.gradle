plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id "kotlin-kapt"
    id "kotlin-parcelize"
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'org.jlleitschuh.gradle.ktlint'
}

task copyPreCommit(type: Copy) {
    from file("../.githooks/pre-commit")
    into file("../.git/hooks")
}
gradle.projectsEvaluated {
    preBuild.dependsOn(copyPreCommit)
}

android {
    compileSdkVersion 33
    buildToolsVersion "33.0.0"

    buildFeatures {
        viewBinding true
    }

    defaultConfig {
        applicationId "hibernate.v2.testyourandroid"
        minSdkVersion 19
        targetSdkVersion 33
        versionCode 1911442
        versionName "Red Bean Ice 11.4.4"
        multiDexEnabled true
        flavorDimensions "default"
        wearAppUnbundled true
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        Properties properties = new Properties()
        properties.load(project.rootProject.file('secrets.properties').newDataInputStream())

        debug {
            debuggable true
            minifyEnabled false
            shrinkResources false
            buildConfigField "String", "ADMOB_BANNER_ID", properties.getProperty('key.admob_banner_id')
            buildConfigField "String", "ADMOB_HOME_AD_ID", properties.getProperty('key.admob_home_ad_id')
            buildConfigField "String", "ADMOB_FULL_SCREEN_ID", properties.getProperty('key.admob_full_screen_id')
            buildConfigField "String", "ADMOB_APP_ID", properties.getProperty("key.admob_app_id")
            resValue "string", "ADMOB_APP_ID", properties.getProperty("key.admob_app_id")
            resValue "string", "GOOGLE_API_KEY", properties.getProperty("key.google_api_key")
            buildConfigField "String", "CONTACT_EMAIL", properties.getProperty("key.contact_email")
            buildConfigField "String", "CROWDIN_URL", properties.getProperty("key.crowdin_url")
        }
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            buildConfigField "String", "ADMOB_BANNER_ID", properties.getProperty('key.admob_banner_id')
            buildConfigField "String", "ADMOB_HOME_AD_ID", properties.getProperty('key.admob_home_ad_id')
            buildConfigField "String", "ADMOB_FULL_SCREEN_ID", properties.getProperty('key.admob_full_screen_id')
            buildConfigField "String", "ADMOB_APP_ID", properties.getProperty("key.admob_app_id")
            resValue "string", "ADMOB_APP_ID", properties.getProperty("key.admob_app_id")
            resValue "string", "GOOGLE_API_KEY", properties.getProperty("key.google_api_key")
            buildConfigField "String", "CONTACT_EMAIL", properties.getProperty("key.contact_email")
            buildConfigField "String", "CROWDIN_URL", properties.getProperty("key.crowdin_url")
        }
    }

    kotlinOptions {
        jvmTarget = '11'
        // Opt-in option for Koin annotation of KoinComponent.
        freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
}

dependencies {
    implementation project(':draw-library')

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'

    // Google Android Support
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.activity:activity-ktx:1.6.1'
    implementation 'androidx.annotation:annotation:1.5.0'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'androidx.biometric:biometric:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.fragment:fragment-ktx:1.5.5'
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.preference:preference-ktx:1.2.0"
    implementation "androidx.viewpager2:viewpager2:1.0.0"
    implementation "androidx.work:work-runtime-ktx:2.7.1"

    // Koin for Android
    implementation "io.insert-koin:koin-android:$koin_version"

    // https://developer.android.com/google/play/billing/billing_library_overview
    implementation 'com.android.billingclient:billing-ktx:4.0.0'

    // Google Play Services
    implementation 'com.google.android.gms:play-services-ads:21.4.0'
    implementation 'com.google.android.gms:play-services-location:21.0.1'
    implementation 'com.google.android.gms:play-services-maps:18.1.0'
    implementation 'com.google.android.material:material:1.7.0'
    implementation platform('com.google.firebase:firebase-bom:31.1.1')
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'

    // https://github.com/coil-kt/coil
    implementation 'io.coil-kt:coil:1.4.0'

    // https://github.com/PSDev/LicensesDialog
    implementation 'de.psdev.licensesdialog:licensesdialog:2.2.0'

    // https://github.com/natario1/CameraView
    implementation 'com.otaliastudios:cameraview:2.7.2'

    // https://github.com/yuriy-budiyev/code-scanner
    implementation 'com.github.yuriy-budiyev:code-scanner:2.1.0'

    // https://github.com/javiersantos/AppUpdater
    implementation 'com.github.javiersantos:AppUpdater:2.7'

    // https://github.com/himphen/logger
    implementation 'com.github.himphen:logger:3.0.1'

    implementation 'com.jjoe64:graphview:4.2.2'

    // https://github.com/skydoves/AndroidVeil
    implementation 'com.github.skydoves:androidveil:1.1.2'
}